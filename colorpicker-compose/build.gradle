import com.github.skydoves.colorpicker.compose.Configuration
import com.github.skydoves.colorpicker.compose.Dependencies
import com.github.skydoves.colorpicker.compose.Versions

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.dokka'
    id 'binary-compatibility-validator'
    id 'maven-publish'
}

android {
    compileSdkVersion Configuration.compileSdk
    defaultConfig {
        minSdkVersion Configuration.minSdk
        targetSdkVersion Configuration.targetSdk
    }

    buildFeatures {
        compose true
        buildConfig false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    composeOptions {
        kotlinCompilerExtensionVersion Versions.COMPOSE_COMPILER
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    lintOptions {
        abortOnError false
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.freeCompilerArgs += [
            "-Xexplicit-api=strict",
            "-Xopt-in=androidx.compose.ui.ExperimentalComposeUiApi"
    ]
}

dependencies {
    implementation Dependencies.androidxCoreKtx

    implementation platform(Dependencies.composeBom)
    implementation Dependencies.composeUI
    implementation Dependencies.composeRuntime
    implementation Dependencies.composeFoundation
    implementation Dependencies.composeTooling
}

afterEvaluate {
    android.libraryVariants.each { variant ->
        publishing.publications.create(variant.name, MavenPublication){
            from components.findByName(variant.name)

            groupId = 'com.kristalcraft'
            artifactId = 'colorpicker-compose'
            version = '1.0.2'
        }
    }
}